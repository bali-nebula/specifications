!>
A formal definition of Bali Wirth Syntax Notation™ (BWSN) using Bali Wirth
Syntax Notation™ itself. The token names are identified by all CAPITAL
characters and the rule names are identified by lowerCamelCase characters.
The starting rule is $syntax.
<!

$syntax: <production | COMMENT> EOF  ! EOF is the end-of-file marker.

$COMMENT: "!>" EOL  {COMMENT | ~"<!"} EOL {SPACE} "<!"  ! Allows recursion.

$EOL: "\n"

$SPACE: " "  ! All spaces are ignored.

$production: SYMBOL ":" rule [NOTE] <EOL>

$SYMBOL: "$" IDENTIFIER

$IDENTIFIER: LETTER {LETTER | DIGIT}

$NOTE: "! " {~EOL}

$rule: <factor> {"|" [[NOTE] EOL] <factor>}

$factor:
    CATAGORY               |
    IDENTIFIER             |
    literal [".." literal] |
    "~" factor             |  ! Indicates NOT the factor.
    "(" rule ")"           |  ! Indicates that the rule is evaluated first.
    "[' rule "]"           |  ! Indicates zero or one repetitions of the rule.
    "{" rule "}"           |  ! Indicates zero or more repetitions of the rule.
    "<' rule ">"              ! Indicates one or more repetitions of the rule.

$CATAGORY: "LETTER" | "DIGIT"  ! The Unicode code points catagories L and N.

$literal: "'" <~"'"> "'" | '"' <~'"'> '"'

