!>
    A formal definition of Bali Wirth Syntax Notation (BWSN) using
    Bali Wirth Syntax Notation itself.
<!

$syntax: <production | COMMENT>

$production: SYMBOL ":" rule [NOTE] <EOL>

$rule: <factor> {"|" [[NOTE] EOL] <factor>}

$factor:
    CATAGORY               |
    IDENTIFIER             |
    literal [".." literal] |
    "~" factor             |  ! Indicates NOT the factor.
    "(" rule ")"           |  ! Indicates that the rule is evaluated first.
    "[' rule "]"           |  ! Indicates zero or one repetitions of the rule.
    "{" rule "}"           |  ! Indicates zero or more repetitions of the rule.
    "<' rule ">"              ! Indicates one or more repetitions of the rule.

$literal: "'" <~"'"> "'" | '"' <~'"'> '"'


!>
    TOKEN DEFINITIONS
<!

$SYMBOL: "$" IDENTIFIER

$IDENTIFIER: LETTER {LETTER | DIGIT}

$CATAGORY: "LETTER" | "DIGIT"  ! The Unicode code points catagories L and N.

$NOTE: "! " {~EOL}

$COMMENT: "!>" EOL  {COMMENT | ~"<!"} EOL {SPACE} "<!"  ! Allows recursion.

$EOL: "\n"

$SPACE: " "  ! All spaces are ignored.

